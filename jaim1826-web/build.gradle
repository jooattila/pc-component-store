plugins {
    id 'war'
}

dependencies {
    implementation project(':jaim1826-backend')
    // https://mvnrepository.com/artifact/jakarta.servlet/jakarta.servlet-api
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '5.0.0'
// https://mvnrepository.com/artifact/com.github.jknack/handlebars
    implementation group: 'com.github.jknack', name: 'handlebars', version: '4.3.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'

}

def deploymentName = 'component'
// beolvassuk környezeti változóból, hogy hova van telepítve a Tomcat
def tomcatHome = System.getenv('CATALINA_HOME')
logger.quiet("Tomcat home detected as '${tomcatHome}'")

// ezzel a taszkkal telepítjük ki a wart a Tomcatre
task deploy(type: Copy, dependsOn: 'war') {
// futás előtt megnézzük, hogy a Tomcat home-ja helyesen be van-e állítva
    doFirst {
        if (!tomcatHome) throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
    }
// a "war" task kimenetét másoljuk
    from war.archiveFile
// átnevezzük a war állományt a megfelelő contextRoot kialakításáért
    rename war.archiveFileName.get(), "${deploymentName}.war"
// a másolás célpontja a Tomcat "webapps" almappája
    into "${tomcatHome}/webapps"
}
// Tomcat kitelepítés törlése
task undeploy(type: Delete) {
// futás előtt megnézzük, hogy a Tomcat home-ja helyesen be van-e állítva
    doFirst {
        if (!tomcatHome) throw new InvalidUserDataException("Tomcat home (CATALINA_HOME) not set")
    }
// töröljük a kitelepített war file-t
    delete "${tomcatHome}/webapps/${deploymentName}.war"
}